
%start Source
%title "Sdc grammar"
%comment "Empty grammar generated by `parol`"
%auto_newline_off

%scanner BraceGroup {
    %auto_newline_off
    %auto_ws_off
}

%%

// ----------------------------------------------------------------------------
// Terminal
// ----------------------------------------------------------------------------

// Longest match should be first

TermLBracket                   : '[';
TermRBracket                   : ']';

TermLBrace                     : <BraceGroup, INITIAL>'{';
TermRBrace                     : <BraceGroup, INITIAL>'}';

TermStringGroup                : "\u{0022}(?:\\[\u{0022}\\/bfnrt]|u[0-9a-fA-F]{4}|[^\u{0022}\\]|\\\n)*\u{0022}";

TermComment                    : /#.*(\r\n|\r|\n|$)/;
TermSemiColon                  : ';';
TermBackslashLineBreak         : /\\(\r\n|\r|\n)/;
TermLineBreak                  : /(\r\n|\r|\n|$)/;
TermWord                       : /[^\s\[\]\\;]+/;

TermBraceGroupContent          : <BraceGroup>/[^}]*/;
TermBraceGroup                 : TermLBrace %push(BraceGroup) (TermBraceGroup | TermBraceGroupContent) TermRBrace %pop();

// ----------------------------------------------------------------------------
// Token
// ----------------------------------------------------------------------------

TokenEnd: TermLineBreak | TermSemiColon;

TokenBraceGroup : TermBraceGroup  [ TermBackslashLineBreak ];
TokenStringGroup: TermStringGroup [ TermBackslashLineBreak ];
TokenLBracket   : TermLBracket    [ TermBackslashLineBreak ];
TokenRBracket   : TermRBracket    [ TermBackslashLineBreak ];
TokenWord       : TermWord        [ TermBackslashLineBreak ];

// ----------------------------------------------------------------------------
// Source
// ----------------------------------------------------------------------------

Argument: TokenWord | TokenStringGroup | TokenBraceGroup | CommandSubstitution;

CommandSubstitution: TokenLBracket Command TokenRBracket;

Command: TokenWord { Argument };

CommandLine: Command TokenEnd;

Source: { CommandLine | TokenEnd | TermComment };
